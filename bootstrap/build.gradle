import groovy.json.JsonBuilder

import java.nio.charset.Charset
import java.security.DigestInputStream
import java.security.MessageDigest
import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform


plugins {
    id 'java'
    id "com.github.johnrengelman.shadow"
    id "org.openjfx.javafxplugin"
    id "com.github.gmazzo.buildconfig"
}

apply from: '../gradle/java.gradle'
apply from: '../gradle/javafx.gradle'

sourceSets {
    modern
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileModernJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    modernCompileOnly(sourceSets.main.output)
    modernCompileOnly(project(":bridge"))

    implementation(project(":bridge"))
    implementation(project(":common"))
    implementation(libs.raven)
    implementation(libs.jopt.simple)
    implementation(libs.java.statsd.client)
    implementation(libs.java.semver)
    implementation(libs.commons.lang3)
    implementation(libs.commons.compress)
    implementation(libs.gson)
    implementation(libs.commons.io)
    implementation(libs.bundles.jna)
    implementation(libs.flatlaf)
    implementation(libs.slf4j.api)
    implementation(libs.slf4j.simple)
    implementation(libs.oshi)

    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.mockito.core)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testRuntimeOnly(libs.mockito.junit.jupiter)
    testRuntimeOnly(libs.mockito.inline)
}

shadowJar {
    configurations = [
            project.configurations.runtimeClasspath,
            project.configurations.modernRuntimeClasspath,
    ]
    relocate("com.", "shaded.com.") {
        exclude("com.sun.**")
        exclude("/com/sun/**")
        exclude("/com/apple/laf/**")
        exclude("com.formdev.**")
        exclude("com.rm5248.dbusjava.**")
        exclude("/com/rm5248/dbusjava/**")
        exclude("com.kenai.**")
        exclude("/com/kenai/**")
        exclude("com.feralinteractive.**")
        exclude("/com/feralinteractive/**")
        exclude("com.oshi.**")
    }
    relocate("de.", "shaded.de.")
    relocate("io.", "shaded.io.")
//    relocate("joptsimple.", "shaded.joptsimple.")
    relocate("org.", "shaded.org.") {
        exclude("org.freedesktop.**")
        exclude("/org/freedesktop/**")
        exclude("org.newsclub.**")
        exclude("org.slf4j.**")
    }
    relocate("ru.turikhay.util.windows.wmi", "shaded.ru.turikhay.util.windows.wmi")
    relocate("net.", "shaded.net.") {
        exclude("net.hadess.**")
        exclude("/net/hadess/**")
    }

    exclude '*.md'
    exclude '*module-info.class'
    exclude 'LICENSE'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'

    dependencies {
        exclude(dependency('org.openjfx:.*'))
    }

    into('META-INF/versions/11') {
        from sourceSets.modern.output
    }

    manifest.attributes(
            'Main-Class': 'ru.turikhay.tlauncher.bootstrap.BootstrapStarter',
            'Multi-Release': true
    )
}

task runDebug(type: JavaExec) {
    group = "Execution"
    description = "Run BootstrapDebug"
    classpath(sourceSets.modern.runtimeClasspath, sourceSets.main.runtimeClasspath, sourceSets.test.runtimeClasspath)
    maxHeapSize = "256M"
    mainClass.set("ru.turikhay.tlauncher.bootstrap.BootstrapDebug")
    if (System.getenv("JRE_EXECUTABLE")) {
        executable(System.getenv("JRE_EXECUTABLE"))
    }
    if (System.getenv("RUN_EXTERNAL") == "true") {
        jvmArgs("-Dtlauncher.bootstrap.debug.external=true")
    }
    if (System.getenv("UI_SCALE")) {
        jvmArgs("-Dsun.java2d.uiScale=" + System.getenv("UI_SCALE"))
    }
    jvmArgs("-Dtlauncher.logFolder=${rootDir}/logs")
    jvmArgs("-Dtlauncher.systemCharset=${Charset.defaultCharset().name()}")
    if (DefaultNativePlatform.currentOperatingSystem.macOsX) {
        jvmArgs("-Dapple.awt.application.appearance=system")
    }
    args("--debug")

    dependsOn(":launcher:buildLauncherRepo")

    if (System.getenv("JRE_LEGACY") == "true") {
        javaLauncher.set javaToolchains.launcherFor {
            languageVersion.set JavaLanguageVersion.of(8)
        }
        dependsOn(":launcher:prepareLauncherDirJava8")
    } else {
        dependsOn(":launcher:prepareLauncherDir")
    }
}

static def generateChecksum(File file) {
    file.withInputStream {
        new DigestInputStream(it, MessageDigest.getInstance('SHA-256')).withStream {
            it.eachByte {}
            it.messageDigest.digest().encodeHex() as String
        }
    }
}

buildConfig {
    className("BuildConfig")
    packageName("ru.turikhay.tlauncher.bootstrap")

    useJavaOutput()

    buildConfigField('String', 'SHORT_BRAND', "\"${short_brand}\"")
    buildConfigField('String', 'FULL_BRAND', "\"${full_brand}\"")
    buildConfigField('String', 'VERSION', "\"${product_version}\"")
}

processResources {
    def meta = [
            version   : product_version,
            shortBrand: short_brand,
            brand     : full_brand,
    ]

    inputs.property("meta", meta)

    doLast {
        new File(destinationDir, "ru/turikhay/tlauncher/bootstrap/meta.json").text = new JsonBuilder(meta).toString()
    }
}

task generateUpdateJson {
    dependsOn shadowJar
    doLast {
        def jarFileChecksum = generateChecksum(shadowJar.outputs.files.singleFile)

        def downloadPath = "repo/update/${short_brand}/bootstrap/${jarFileChecksum}.jar"

        def meta = [
                version : product_version,
                checksum: jarFileChecksum,
                url     : [ "https://llaun.ch/${downloadPath}" ] + repo_hosts.collect { host ->
                    ["http", "https"].collect {scheme ->
                        "${scheme}://${host}/${downloadPath}"
                    }
                }.flatten().sort { a, b ->
                    a.startsWith("https") ? (b.startsWith("https") ? 0 : -1) : (b.startsWith("https") ? 1 : 0)
                }
        ]

        file("${buildDir}/update/${short_brand}/bootstrap.json").write(new JsonBuilder(meta).toString())
    }
}

task copyJarAndRename(type: Copy) {
    dependsOn shadowJar
    from shadowJar.outputs.files.singleFile
    into "${buildDir}/update/${short_brand}"
    rename { "bootstrap.jar" }
}

task generateSha256File {
    dependsOn shadowJar
    doLast {
        file("${buildDir}/update/${short_brand}/bootstrap.jar.sha256").write(generateChecksum(shadowJar.outputs.files.singleFile))
    }
}

assemble {
    dependsOn(generateUpdateJson, copyJarAndRename, generateSha256File)
}

test {
    useJUnitPlatform()
}
