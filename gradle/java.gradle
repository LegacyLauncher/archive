java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    toolchain {
        languageVersion = JavaLanguageVersion.of(buildUsingJava)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.convention(8)
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url("https://cdn.turikhay.ru/tlauncher/repo/libraries")
        name("turikhay cdn")
        metadataSources {
            mavenPom()
            artifact()
        }
        mavenContent {
            includeGroup("me.cortex")
            includeGroup("ru.turikhay")
            includeGroup("ru.turikhay.app")
        }
    }
    maven {
        url("https://libraries.minecraft.net")
        name("Minecraft")
        mavenContent {
            includeGroup("com.mojang")
        }
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}
