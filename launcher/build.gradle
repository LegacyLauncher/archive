import groovy.json.JsonBuilder
import org.apache.tools.ant.filters.ReplaceTokens

import java.security.DigestInputStream
import java.security.MessageDigest
import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id "org.ajoberstar.grgit"
    id "org.openjfx.javafxplugin"
}

version '1.135.2'

apply from: '../gradle/brand.gradle'
apply from: '../gradle/javafx.gradle'

sourceSets {
    modern
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileModernJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

configurations {
    tlRuntime
    implementation.extendsFrom(tlRuntime)
}

def authlibVersion = "1.5.24"
def nstweakerVersion = "1.0"

dependencies {
    modernImplementation(sourceSets.main.output)

    tlRuntime("com.google.guava:guava:14.0")
    tlRuntime("io.sentry:sentry:1.7.30")
    tlRuntime("org.slf4j:slf4j-api:1.7.21")
    tlRuntime("com.fasterxml.jackson.core:jackson-core:2.12.0")
    tlRuntime("com.github.zafarkhaja:java-semver:0.9.0")
    tlRuntime("com.google.code.gson:gson:2.8.7")
    tlRuntime("commons-io:commons-io:2.5")
    tlRuntime("net.sf.jopt-simple:jopt-simple:4.9")
    tlRuntime("org.apache.commons:commons-compress:1.10")
    tlRuntime("org.tukaani:xz:1.5")
    tlRuntime("org.jdom:jdom:2.0.2")
    tlRuntime("org.apache.commons:commons-lang3:3.4")
    tlRuntime("com.timgroup:java-statsd-client:3.1.0")
    tlRuntime("org.apache.logging.log4j:log4j-api:2.15.0")
    tlRuntime("org.apache.logging.log4j:log4j-core:2.15.0")
    tlRuntime("org.apache.logging.log4j:log4j-1.2-api:2.15.0")
    tlRuntime("org.apache.httpcomponents:httpclient:4.5.13")
    tlRuntime("org.apache.httpcomponents:fluent-hc:4.5.13")
    tlRuntime('org.nanohttpd:nanohttpd:2.3.1')
    tlRuntime('ru.turikhay:jvd:1.0')
    tlRuntime('com.moandjiezana.toml:toml4j:0.7.2')
    annotationProcessor("org.apache.logging.log4j:log4j-core:2.15.0")
    implementation(files("${rootProject.rootDir}/libraries/maven/jvd-1.0.jar"))
    implementation(files("${rootProject.rootDir}/libraries/nstweaker-${nstweakerVersion}.jar"))
    implementation(files("${rootProject.rootDir}/libraries/authlib-${authlibVersion}.jar"))
    implementation(project(":common"))
    implementation(project(":bridge"))

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.1')
    testImplementation('org.mockito:mockito-core:3.12.4')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.8.1')
    testRuntimeOnly('org.mockito:mockito-junit-jupiter:3.12.4')
    testRuntimeOnly('org.mockito:mockito-inline:3.12.4')
}

task cleanLauncherRepo(type: Delete) {
    doLast {
        delete file("${rootProject.rootDir}/lib")
    }
}

task buildLauncherRepo(type: Sync) {
    doFirst {
        mkdir(file("${rootProject.rootDir}/lib"))
    }

    dependsOn(cleanLauncherRepo)

    destinationDir = file("${rootProject.rootDir}/lib")
    configurations.tlRuntime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        def path = artifact.moduleVersion.id.with {
            "${group.replace('.', '/')}/${name}/$version"
        }
        into(path) {
            from(artifact.file)
        }
    }

    from(file("${rootProject.rootDir}/libraries/nstweaker-${nstweakerVersion}.jar")) {
        into("ru/turikhay/app/nstweaker/${nstweakerVersion}")
    }

    from(file("${rootProject.rootDir}/libraries/authlib-${authlibVersion}.jar")) {
        into("com/mojang/authlib/${authlibVersion}")
    }
}

task prepareLauncherDir(type: Sync) {
    doFirst {
        delete(file("build/tlRun"))
        mkdir(file("build/tlRun"))
    }

    destinationDir = file("build/tlRun")
    dependsOn(compileModernJava)

    from(compileModernJava)
    from(compileJava)
    from(sourceSets.modern.resources)
    from(sourceSets.main.resources)

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task prepareLauncherDirJava8(type: Sync) {
    doFirst {
        delete(file("build/tlRun"))
        mkdir(file("build/tlRun"))
    }

    destinationDir = file("build/tlRun")
    dependsOn(compileJava)

    from(compileJava)
    from(sourceSets.main.resources)
}

static def generateChecksum(File file) {
    file.withInputStream {
        new DigestInputStream(it, MessageDigest.getInstance('SHA-256')).withStream {
            it.eachByte {}
            it.messageDigest.digest().encodeHex() as String
        }
    }
}

task generateMeta {
    doLast {
        def main_class = System.getenv("MAIN_CLASS")
        if (!main_class) main_class = "ru.turikhay.tlauncher.TLauncher"

        def libs = []
        configurations.tlRuntime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            libs.add([name: artifact.moduleVersion.id.toString(), checksum: generateChecksum(artifact.file)])
        }
        libs.add([name: "com.mojang:authlib:${authlibVersion}", checksum: generateChecksum(file("${rootProject.rootDir}/libraries/authlib-${authlibVersion}.jar"))])
        libs.add([name: "ru.turikhay.app:nstweaker:${nstweakerVersion}", checksum: generateChecksum(file("${rootProject.rootDir}/libraries/nstweaker-${nstweakerVersion}.jar"))])

        def meta = new JsonBuilder()

        meta {
            version(product_version)
            shortBrand(short_brand)
            brand(full_brand)
            buildDate(OffsetDateTime.now(ZoneOffset.UTC).format(DateTimeFormatter.ISO_DATE_TIME))
            libraries(libs)
            mainClass(main_class)
        }

        file("$projectDir/src/main/resources/ru/turikhay/tlauncher/meta.json").write(meta.toString())
        file("$projectDir/meta.json").write(meta.toPrettyString())
    }
}

task generateUpdateJson {
    dependsOn(jar, buildLauncherRepo)
    doLast {
        def meta = new JsonBuilder()
        def jarFileChecksum = generateChecksum(jar.outputs.files.singleFile)
        def downloadUrlList = [
                "https://tlauncherrepo.com/",
                "https://u.tlauncher.ru/",
                "https://repo.tlaun.ch/",
                "https://cdn.turikhay.ru/tlauncher/",
                "http://cdn.turikhay.ru/tlauncher/"
        ].collect {
            urlPrefix -> urlPrefix + short_brand + "/launcher/" + jarFileChecksum + ".jar"
        }

        meta {
            version product_version
            shortBrand short_brand
            checksum jarFileChecksum
            downloads downloadUrlList
        }

        file("${buildDir}/update/launcher.json").write(meta.toString())

        def metaModern = new JsonBuilder() // lol
        metaModern {
            version product_version
            checksum jarFileChecksum
            url downloadUrlList
            description {
                ru_RU ""
                en_US ""
            }
        }

        file("${buildDir}/update/launcher_modern.json").write(metaModern.toString())
    }
}

task copyJarAndRenameToSha256(type: Copy) {
    dependsOn jar
    from jar.outputs.files.singleFile
    into "${buildDir}/update"
    rename { generateChecksum(jar.outputs.files.singleFile) + ".jar" }
}

task generateSources(type: Copy) {
    from "src/main/java"
    from "$rootDir/common/src/main/java"
    into "$buildDir/generated"
    filter(ReplaceTokens, tokens: [
            'tl_short_brand': short_brand,
            'tl_brand': full_brand
    ])
}

compileJava {
    source = generateSources.destinationDir
    dependsOn(generateSources, generateMeta)
}

assemble {
    dependsOn(generateUpdateJson, copyJarAndRenameToSha256)
}

jar {
    into('META-INF/versions/11') {
        from sourceSets.modern.output
    }

    manifest {
        attributes (
                'Multi-Release': true
        )
    }
}

test {
    useJUnitPlatform()
}
