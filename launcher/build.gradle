import groovy.json.JsonBuilder

import java.security.DigestInputStream
import java.security.MessageDigest
import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id "org.ajoberstar.grgit"
    id "org.openjfx.javafxplugin"
    id "com.github.gmazzo.buildconfig"
}

apply from: '../gradle/brand.gradle'
apply from: '../gradle/javafx.gradle'

sourceSets {
    modern
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileModernJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

configurations {
    implementation.extendsFrom(tlRuntime)
}

dependencies {
    modernImplementation(sourceSets.main.output)

    tlRuntime(libs.guava)
    tlRuntime(libs.sentry)
    tlRuntime(libs.slf4j.api)
    tlRuntime(libs.jackson.core)
    tlRuntime(libs.java.semver)
    tlRuntime(libs.gson)
    tlRuntime(libs.commons.io)
    tlRuntime(libs.jopt.simple)
    tlRuntime(libs.commons.compress)
    tlRuntime(libs.xz)
    tlRuntime(libs.jdom)
    tlRuntime(libs.commons.lang3)
    tlRuntime(libs.java.statsd.client)
    tlRuntime(libs.bundles.log4j)
    tlRuntime(libs.bundles.httpcomponents)
    tlRuntime(libs.nanohttpd)
    tlRuntime(libs.toml4j)
    tlRuntime(libs.authlib)
    tlRuntime(libs.jvd)
    tlRuntime(libs.nstweaker)
    annotationProcessor(libs.log4j.core)
    implementation(project(":common"))
    implementation(project(":bridge"))

    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.mockito.core)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testRuntimeOnly(libs.mockito.junit.jupiter)
    testRuntimeOnly(libs.mockito.inline)
}

task buildLauncherRepo(type: Sync) {
    destinationDir = file("${rootProject.rootDir}/lib")
    configurations.tlRuntime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        def path = artifact.moduleVersion.id.with {
            "${group.replace('.', '/')}/${name}/$version"
        }
        into(path) {
            from(artifact.file)
        }
    }
}

task prepareLauncherDir(type: Sync) {
    destinationDir = file("${buildDir}/tlRun")
    dependsOn(compileModernJava)

    from(compileModernJava)
    from(compileJava)
    from(sourceSets.modern.resources)
    from(sourceSets.main.resources)

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task prepareLauncherDirJava8(type: Sync) {
    destinationDir = file("${buildDir}/tlRun")
    dependsOn(compileJava)

    from(compileJava)
    from(sourceSets.main.resources)
}

static def generateChecksum(File file) {
    file.withInputStream {
        new DigestInputStream(it, MessageDigest.getInstance('SHA-256')).withStream {
            it.eachByte {}
            it.messageDigest.digest().encodeHex() as String
        }
    }
}

processResources {
    def meta = [
            version   : product_version,
            shortBrand: short_brand,
            brand     : full_brand,
            buildDate : OffsetDateTime.now(ZoneOffset.UTC).format(DateTimeFormatter.ISO_DATE_TIME),
            libraries : configurations.tlRuntime.resolvedConfiguration.resolvedArtifacts.collect { artifact ->
                [name: artifact.moduleVersion.id.toString(), checksum: generateChecksum(artifact.file)]
            },
            mainClass : System.getenv("MAIN_CLASS") ?: "ru.turikhay.tlauncher.TLauncher"
    ]

    inputs.property("meta", meta)

    doLast {
        new File(destinationDir, "ru/turikhay/tlauncher/meta.json").text = new JsonBuilder(meta).toString()
    }
}

task generateUpdateJson {
    dependsOn(jar)

    doLast {
        def jarFileChecksum = generateChecksum(jar.outputs.files.singleFile)
        def downloadUrlList = [
                "https://tlauncherrepo.com/",
                "https://tln4.ru/",
                "https://repo.tlaun.ch/",
                "https://cdn.turikhay.ru/tlauncher/",
                "http://cdn.turikhay.ru/tlauncher/"
        ].collect {
            urlPrefix -> urlPrefix + short_brand + "/launcher/" + jarFileChecksum + ".jar"
        }

        def meta = [
                version   : product_version,
                shortBrand: short_brand,
                checksum  : jarFileChecksum,
                downloads : downloadUrlList
        ]

        file("${buildDir}/update/launcher.json").write(new JsonBuilder(meta).toString())

        def metaModern = [
                version    : product_version,
                checksum   : jarFileChecksum,
                url        : downloadUrlList,
                description: [
                        ru_RU: "",
                        en_US: ""
                ]
        ]

        file("${buildDir}/update/launcher_modern.json").write(new JsonBuilder(metaModern).toString())
    }
}

task copyJarAndRenameToSha256(type: Copy) {
    from jar
    into "${buildDir}/update"
    rename { generateChecksum(jar.outputs.files.singleFile) + ".jar" }
}

buildConfig {
    className("BuildConfig")
    packageName("ru.turikhay.tlauncher.configuration")

    useJavaOutput()

    buildConfigField('String', 'SHORT_BRAND', "\"${short_brand}\"")
    buildConfigField('String', 'FULL_BRAND', "\"${full_brand}\"")
    buildConfigField('String', 'VERSION', "\"${product_version}\"")
}

assemble {
    dependsOn(generateUpdateJson, copyJarAndRenameToSha256)
}

jar {
    into('META-INF/versions/11') {
        from sourceSets.modern.output
    }

    manifest.attributes(
            'Multi-Release': true
    )
}

test {
    useJUnitPlatform()
}
